buildscript {
    ext {
        springBootVersion = '1.4.1.RELEASE'
        gradleWraperVersion = '3.1'
    }
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
    }
}

apply plugin: 'java'
apply plugin: 'spring-boot'

dependencyManagement {
    imports {
        mavenBom 'org.springframework.cloud:spring-cloud-dependencies:Camden.RELEASE'
    }
}

repositories {
    mavenCentral()
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

group = 'es.manuel.tdd'
version = '0.0.1-SNAPSHOT'

dependencies {
    compile 'org.springframework.boot:spring-boot-devtools'
    compile 'org.springframework.cloud:spring-cloud-starter-eureka-server'
    testCompile 'org.springframework.boot:spring-boot-starter-test'
}

jar {
    archiveName = 'game-of-life.jar'
}

task bower(type: Exec) {
    workingDir "$project.rootDir/src/main/webapp"
    commandLine 'cmd', '/c', 'bower install'
}

task polymer(type: Exec) {
    workingDir "$project.rootDir/src/main/webapp"
    commandLine 'cmd', '/c', 'polymer build'
}

task copyPolymer(type: Copy) {
    from "$project.rootDir/src/main/webapp/build/unbundled"
    into "$project.rootDir/build/resources/main/static"
}

polymer.dependsOn bower
copyPolymer.dependsOn polymer
processResources.finalizedBy copyPolymer

task wrapper(type: Wrapper) {
    gradleVersion = gradleWraperVersion
}